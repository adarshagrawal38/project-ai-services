pipeline {
    agent { label 'ai-services-4-spyre-card-1' }

    parameters {
        string(name: 'PR_NUMBER', description: 'GitHub Pull Request number')
    }

    stages {

        stage('Checkout PR') {
            steps {
                script {
                    echo "Fetching PR #${params.PR_NUMBER}"

                    // Clean workspace
                    deleteDir()

                    // Clone repo and checkout PR
                    sh """
                        git clone https://github.com/IBM/project-ai-services.git repo
                        cd repo
                        git fetch origin pull/${params.PR_NUMBER}/head:pr-${params.PR_NUMBER}
                        git checkout pr-${params.PR_NUMBER}
                    """
                }
            }
        }

        stage('Detect Changes') {
            steps {
                script {
                    dir('repo') {
                        // Compare with main branch
                        sh 'git fetch origin main'
                        def changedFiles = sh(script: 'git diff --name-only origin/main', returnStdout: true).trim().split('\n')

                        // Declare as local variables
                        def backendChanged = changedFiles.any { it.startsWith('spyre-rag/src/') }
                        def uiChanged = changedFiles.any { it.startsWith('spyre-rag/ui/') }

                        echo "Backend changed: ${backendChanged}"
                        echo "UI changed     : ${uiChanged}"

                        if (!backendChanged && !uiChanged) {
                            error 'No UI or backend changes detected â€” stopping pipeline'
                        }

                        // Save for later stages
                        env.BACKEND_CHANGED = backendChanged.toString()
                        env.UI_CHANGED = uiChanged.toString()
                    }
                }
            }
        }

        stage('Build Local Images') {
            steps {
                script {
                    dir('repo') {
                        def sha = sh(
                            script: 'git rev-parse --short HEAD',
                            returnStdout: true
                        ).trim()
        
                        // Registry value is required by Makefile but we don't push
                        def localRegistry = "localhost/local/pr-${params.PR_NUMBER}"
        
                        if (env.BACKEND_CHANGED.toBoolean()) {
                            env.BACKEND_IMAGE = "${localRegistry}/rag"
                            dir('spyre-rag/src') {
                                // Build backend image
                                sh "make build REGISTRY=${localRegistry}"
        
                                // Get the tag from Makefile
                                def tag = sh(script: "make image-tag REGISTRY=${localRegistry}", returnStdout: true).trim()
                                env.BACKEND_IMAGE = "${localRegistry}/rag:${tag}"
                                echo "Backend image set to ${env.BACKEND_IMAGE}"
                            }
                        }
        
                        if (env.UI_CHANGED.toBoolean()) {
                            env.UI_IMAGE = "${localRegistry}/rag-ui"
                            dir('spyre-rag/ui') {
                                // Build UI image
                                sh "make build REGISTRY=${localRegistry}"
        
                                // Get the tag from Makefile
                                def tag = sh(script: "make image-tag REGISTRY=${localRegistry}", returnStdout: true).trim()
                                env.UI_IMAGE = "${localRegistry}/rag-ui:${tag}"
                                echo "UI image set to ${env.UI_IMAGE}"
                            }
                        }
                    }
                }
            }
        }
        stage('Update values.yaml (all templates)') {
            steps {
                script {
                    dir('repo') {
                        def templates = sh(script: 'ls ai-services/assets/applications', returnStdout: true).trim().split('\n')

                        templates.each { tpl ->
                            def valuesFile = "ai-services/assets/applications/${tpl}/values.yaml"
                            if (!fileExists(valuesFile)) {
                                echo "Skipping ${tpl} (no values.yaml)"
                                return
                            }

                            if (env.UI_CHANGED.toBoolean()) {
                                sh "yq e '.ui.image = \"${env.UI_IMAGE}\"' -i ${valuesFile}"
                            }

                            if (env.BACKEND_CHANGED.toBoolean()) {
                                sh "yq e '.backend.image = \"${env.BACKEND_IMAGE}\"' -i ${valuesFile}"
                            }

                            echo "Updated ${valuesFile}"
                        }
                    }
                }
            }
        }

        stage('Build binary and Deploy') {
            steps {
                script {
                    dir('repo/ai-services') {
                        echo 'Building binary, print binary version and Deploying updated application...'
                        sh """
                            make build
                            ./bin/ai-services application create rag-test -t rag-dev
                            ./bin/ai-services --version
                            podman pod ps
                        """
                    }
                }
            }
        }

        stage('Test Deployment') {
            steps {
                echo 'Waiting for deployment to stabilize...'
                sh 'sleep 30s'
            }
        }
    }
}
